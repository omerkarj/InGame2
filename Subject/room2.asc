// room script file

/// START FUEL PUZZLE LOGIC ////
bool collectedFuel[4];
bool correctFuelSequence = true;

function resetFuelSequence() {
  int i = 0;
  while (i < 4) {
    collectedFuel[i] = false;
    i++;
  }
  correctFuelSequence = true;
}

function FuelContainer_AnyClick()
{
  cMick.Walk(195, 663,  eBlock);
  cMick.Say("There's writing on the can...");
  cMick.Say("It reads, 'Some sort of container for the fuel'. hmm...");
}
function FuelContainer_Interact()
{
  cMick.Walk(195, 663,  eBlock);
  FuelContainer.Visible = false;
  cMick.AddInventory(iFuelContainer);
  cMick.Say("This should come in handy");
}

// Checks if we already picked up the container when doing anything with the barrels,
// then performs the appropriate operations based on barrel color
function fillAndCheck(int barrel) {
  collectedFuel[barrel] = true;
  if (barrel == 0) {
    cMick.Walk(395, 690, eBlock);
    if (collectedFuel[1] && collectedFuel[2] && collectedFuel[3] && correctFuelSequence) {
      cMick.AddInventory(iTalkingSubFuel);
      player.ActiveInventory = iTalkingSubFuel;
      cMick.LoseInventory(iFuelContainer);
    }
  }
  else if (barrel == 1) {
    cMick.Walk(490,  690,  eBlock);
    if (!collectedFuel[3]) {
      correctFuelSequence = false;
    }
  }
  else if (barrel == 2) {
    cMick.Walk(590,  690,  eBlock);
    if (!collectedFuel[1] || !collectedFuel[3]) {
      correctFuelSequence = false;
    }
  }
  else if (barrel == 3) {
    cMick.Walk(700, 690, eBlock);
  }
}
function checkForContainer(int barrel) {
  if (!cMick.HasInventory(iTalkingSubFuel)) {
    if (!cMick.HasInventory(iFuelContainer)) {
      cMick.Say("I need to find some sort of container for the fuel...");
      cSal.SpeechView = 4;
      cSal.Say("FUEEEEELLLLLL!!!!!!!!");
      cSal.SpeechView = 3;
    }
    else {
      if (!collectedFuel[barrel]) {
        fillAndCheck(barrel);
        
        int r = Random(3);
        if (r == 0) {
          cMick.Say("Geez, this liquid smells like day old oysters");
        }
        else if (r == 1) {
          cMick.Say("WOW, this smells like something crawled up and died in there...");
        }
        else if (r == 2) {
          cMick.Say("That smell...");
          cMick.Say("That smelly smell...");
        }
        else if (r == 3) {
          cMick.Say("You actually drink this stuff?");
          cMick.Say("It smells like dead giraffe...");
        }
        cSal.Say("Smells good to me!");
      }
      else {
        if (collectedFuel[0] && collectedFuel[1] && collectedFuel[2] && collectedFuel[3]) {
          cMick.Say("The container is already full...");
        }
        else 
        {
          cMick.Say("I already took some liquid from this barrel");
        }
      }
    }
  }
  else {
    cMick.Say("I already got the fuel");
    cSal.SpeechView = 4;
    cSal.Say("FUEEEEELLLLLL!!!!!!!!");
    cSal.SpeechView = 3;
  }
}

// Interactions for fuel barrels
function BarrelYellow_Look()
{
  cMick.Say("It's a yellow fuel barrel");
}
function BarrelGreen_Look()
{
  cMick.Say("It's a green fuel barrel");
}
function BarrelBlack_Look()
{
  cMick.Say("It's a black fuel barrel");
}
function BarrelRed_Look()
{
  cMick.Say("It's a red fuel barrel");
}
function BarrelYellow_Interact()
{
  cMick.Say("It's a yellow fuel barrel");
}
function BarrelGreen_Interact()
{
  cMick.Say("It's a green fuel barrel");
}
function BarrelBlack_Interact()
{
  cMick.Say("It's a black fuel barrel");
}
function BarrelRed_Interact()
{
  cMick.Say("It's a red fuel barrel");
}
function BarrelYellow_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    checkForContainer(0);
  }
}
function BarrelGreen_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    checkForContainer(2);
  }
}
function BarrelBlack_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    checkForContainer(1);
  }
}
function BarrelRed_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    checkForContainer(3);
  }
}
///// END FUEL PUZZLE /////

// tattoo interactions
function Tattoo_Interact()
{
  cSal.SpeechView = 4;
  cSal.Say("Don't even think about it, Derek!");
  cSal.Say("Stay. Away. From the backside...");
  cSal.SpeechView = 3;
}
bool firstLookAtTat = true;
function Tattoo_Look()
{
  if (firstLookAtTat) {
    cMick.Say("Hey, how'd you get that tattoo?");
    cSal.Say("That? Some Guy Brushed it on with Three Pieces of Wood...");
    firstLookAtTat = false;
  }
  else {
    cMick.Say("Does the tattoo have any meaning?");
    cSal.Say("Can't you see? It's a wicked tribal dragon!");
    cSal.Say("Of course, I didn't ever get to see it..");
    cSal.Say("Can't really bend that way..");
  }
}


function room_LeaveLeft()
{
  player.ChangeRoom(1);
}

function room_AfterFadeIn()
{
  cMick.Walk(100, 640,  eBlock);
  if (salHelloDialog) {
    dSubHello.Start();
    salHelloDialog = false;
  }
}
function room_Load()
{
  cMick.x = 0;
  cMick.y = 640;
  cSal.ChangeRoom(2);
}
function room_FirstLoad()
{
  resetFuelSequence();
  RemoveWalkableArea(2);
}

// fuel disposal interactions
function FuelDisposal_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    cMick.Walk(690, 620,  eBlock);
    resetFuelSequence();
    cMick.Say("Let's empty that fuel container...");
    cMick.Say("Here we go.. empty");
  }
}
function FuelDisposal_Look()
{
  cMick.Walk(690, 620,  eBlock);
  cMick.Say("A fuel disposal apparatus");
  cSal.Say("We are all VERY BIG on recycling here, brah!");
}
bool firstTouchDisposal = true;
function FuelDisposal_Interact()
{
  if (firstTouchDisposal) {
    cMick.Walk(690,  620,  eBlock);
    bgAudio.Stop();
    aMouse_trap_snap.Play(eAudioPriorityHigh);
    cMick.Say("AAAHHHHHHH!!!!");
    cMick.Say("MY HAND! IT BURRRNNNSSSS!!!!");
    cSal.Say("Ha! Can't believe you fell for that!");
    cSal.Say("Lolz...");
    aDeath_sound.Play(eAudioPriorityHigh);
    
    justDied = true;
    player.ChangeRoom(1);
    cMick.LoseInventory(iFuelContainer);
    FuelContainer.Visible = true;
    firstTouchDisposal = false;
  }
  else {
    cSal.SpeechView = 4;
    cSal.SayBackground("Really??");
    cSal.SpeechView = 3;
    cMick.Walk(690,  620,  eBlock);
    cMick.Say("No way.. Not falling for that again...");
    cSal.Say("Awwwww...");
  }
}

function hGiantPear_AnyClick()
{
  cMick.Say("It's a giant pear...");
  cMick.Say("How did it get on that rooftop?");
}

function hSalDoor_Interact()
{
  cMick.Walk(870,  525,  eBlock);
  if (salfueled) {
    aMetal_door_open.Play(eAudioPriorityHigh);
    oSubDoor.Visible = false;
    cMick.Walk(960, 490, eBlock);
    cMick.Say("Wait... is it safe to go in?");
    aRunning_sub.Play(eAudioPriorityHigh);
    cSal.Say("DO YOU WANT SOME OF THIS FUEL, DEREK? IT'S AWESOME!!!!!");
    cMick.Say("Crap...");
    cMick.Say("Well, here goes...");
    player.ChangeRoom(3);
  }
  else {
    aMetal_door_locked.Play(eAudioPriorityHigh);
    cMick.Say("S.A.L, can you let me in please?");
    cSal.Say("NO! Get me some fuel and then we can talk...");
  }
}
