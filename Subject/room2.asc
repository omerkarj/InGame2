// room script file

/// START FUEL PUZZLE LOGIC ////
bool collectedFuel[4];
bool correctFuelSequence = true;

function resetFuelSequence() {
  int i = 0;
  while (i < 4) {
    collectedFuel[i] = false;
    i++;
  }
  correctFuelSequence = true;
}

function FuelContainer_AnyClick()
{
  cMick.Say("There's writing on the can...");
  cMick.Say("It reads, 'Some sort of container for the fuel'. hmm...");
}
function FuelContainer_Interact()
{
  cMick.Walk(110,  744,  eBlock);
  FuelContainer.Visible = false;
  cMick.AddInventory(iFuelContainer);
  cMick.Say("This should come in handy");
}

// Checks if we already picked up the container when doing anything with the barrels,
// then performs the appropriate operations based on barrel color
function fillAndCheck(int barrel) {
  collectedFuel[barrel] = true;
  if (barrel == 0) {
    cMick.Walk(800,  640,  eBlock);
    if (collectedFuel[1] && collectedFuel[2] && collectedFuel[3] && correctFuelSequence) {
      cMick.AddInventory(iTalkingSubFuel);
      player.ActiveInventory = iTalkingSubFuel;
      cMick.LoseInventory(iFuelContainer);
    }
  }
  else if (barrel == 1) {
    cMick.Walk(870,  660,  eBlock);
    if (!collectedFuel[3]) {
      correctFuelSequence = false;
    }
  }
  else if (barrel == 2) {
    cMick.Walk(915,  680,  eBlock);
    if (!collectedFuel[1] || !collectedFuel[3]) {
      correctFuelSequence = false;
    }
  }
  else if (barrel == 3) {
    cMick.Walk(965,  705,  eBlock);
  }
}
function checkForContainer(int barrel) {
  if (!cMick.HasInventory(iTalkingSubFuel)) {
    if (!cMick.HasInventory(iFuelContainer)) {
      cMick.Say("I need to find some sort of container for the fuel...");
      cSal.Say("FUEEEEELLLLLL!!!!!!!!");    
    }
    else {
      if (!collectedFuel[barrel]) {
        fillAndCheck(barrel);
        
        int r = Random(3);
        if (r == 0) {
          cMick.Say("Geez, this liquid smells like day old oysters");
        }
        else if (r == 1) {
          cMick.Say("WOW, this smells like something crawled up and died in there...");
        }
        else if (r == 2) {
          cMick.Say("That smell...");
          cMick.Say("That smelly smell...");
        }
        else if (r == 3) {
          cMick.Say("You actually drink this stuff?");
          cMick.Say("It smells like a dead giraffe...");
        }
        cSal.Say("Smells good to me!");
      }
      else {
        if (collectedFuel[0] && collectedFuel[1] && collectedFuel[2] && collectedFuel[3]) {
          cMick.Say("The container is already full...");
        }
        else 
        {
          cMick.Say("I already took some liquid from this barrel");
        }
      }
    }
  }
  else {
    cMick.Say("I already got the fuel");
    cSal.Say("FUEEEEELLLLLL!!!!!!!!");
  }
}

// Interactions for fuel barrels
function BarrelYellow_Look()
{
  cMick.Say("It's a yellow fuel barrel");
}
function BarrelGreen_Look()
{
  cMick.Say("It's a green fuel barrel");
}
function BarrelBlack_Look()
{
  cMick.Say("It's a black fuel barrel");
}
function BarrelRed_Look()
{
  cMick.Say("It's a red fuel barrel");
}
function BarrelYellow_Interact()
{
  cMick.Say("It's a yellow fuel barrel");
}
function BarrelGreen_Interact()
{
  cMick.Say("It's a green fuel barrel");
}
function BarrelBlack_Interact()
{
  cMick.Say("It's a black fuel barrel");
}
function BarrelRed_Interact()
{
  cMick.Say("It's a red fuel barrel");
}
function BarrelYellow_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    checkForContainer(0);
  }
}
function BarrelGreen_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    checkForContainer(2);
  }
}
function BarrelBlack_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    checkForContainer(1);
  }
}
function BarrelRed_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    checkForContainer(3);
  }
}
///// END FUEL PUZZLE /////

// tattoo interactions
function Tattoo_Interact()
{
  cSal.Say("Don't even think about it, Derek!");
  cSal.Say("Stay. Away. From the backside...");
}
bool firstLookAtTat = true;
function Tattoo_Look()
{
  if (firstLookAtTat) {
    cMick.Say("Hey, how'd you get that tattoo?");
    cSal.Say("That? Some Guy Brushed it on with Three Pieces of Wood...");
    firstLookAtTat = false;
  }
  else {
    cMick.Say("Does the tattoo have any meaning?");
    cSal.Say("Can't you see? It's a wicked tribal dragon!");
    cSal.Say("Of course, I didn't ever get to see it..");
    cSal.Say("Can't really bend that way..");
  }
}


function room_LeaveLeft()
{
  player.ChangeRoom(1);
}
function room_LeaveTop()
{
  cMick.Walk(390, 490, eBlock);
  player.ChangeRoom(3);
}

function room_AfterFadeIn()
{
  cMick.Walk(100, 740,  eBlock);
  dSubHello.Start();
}

function room_Load()
{
  cMick.x = 0;
  cMick.y = 740;
  if (!cMick.HasInventory(iTalkingSubFuel)) {
    resetFuelSequence();
    RemoveWalkableArea(2);
  }
}

// fuel disposal interactions
function FuelDisposal_UseInv()
{
  if (player.ActiveInventory == iFuelContainer) {
    resetFuelSequence();
    cMick.Say("Let's empty that fuel container...");
    cMick.Say("Here we go.. empty");
  }
}
function FuelDisposal_Look()
{
  cMick.Say("A fuel disposal apparatus");
  cSal.Say("We are all VERY BIG on recycling here, brah!");
}
bool firstTouchDisposal = true;
function FuelDisposal_Interact()
{
  if (firstTouchDisposal) {
    cMick.Walk(940,  760,  eBlock);
    cMick.Say("AAAHHHHHHH!!!!");
    cMick.Say("MY HAND! IT BURRRNNNSSSS!!!!");
    cSal.Say("Ha! Can't believe you fell for that!");
    cSal.Say("Lolz...");
    player.ChangeRoom(1);
    cMick.LoseInventory(iFuelContainer);
    FuelContainer.Visible = true;
    firstTouchDisposal = false;
  }
  else {
    cSal.Say("Really??");
    cMick.Say("No way.. Not falling for that again...");
    cSal.Say("Awwwww...");
  }
}