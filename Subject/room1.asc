// room script file


// A function that initializes a bunch of stuff.
function initialize_control_panel() { 
  // Centre the control panel
  gPanel.Centre(); 
  // Centre the Restart dialog as well
  gRestartYN.Centre(); 
  if (IsSpeechVoxAvailable()) { 
    // If there is a speech-vox file, then set it to voice and text. It's best to use
    // both whenever possible, for the player's sake.
    Speech.VoiceMode = eSpeechVoiceAndText;
  }
  
  //And now, set all the defaults
  System.Volume = 100;
  sldAudio.Value = System.Volume;
  SetGameSpeed(40);
  sldSpeed.Value = 40;
  if (IsSpeechVoxAvailable()) {
     Speech.VoiceMode = eSpeechVoiceAndText;
     SetSpeechVolume(255);
  }
}

// pillow interactions
function hHotspot1_Look()
{
  if (!cMick.HasInventory(iBedspring)) {
    cMick.Say("Yup, gotta move that pillow to find the spring again..");
  }
  else {
    cMick.Say("Nothing else under the pillow");
  }
}
function hHotspot1_Interact()
{
  cMick.Walk(800, 620, eBlock);
  if (!cMick.HasInventory(iBedspring)) {
    oPillow.Visible = true;
    bedspring.Visible = true;
    cMick.Say("And there it is! What a surprise..");
  }
  else {
    cMick.Say("Nothing else under the pillow");
  }
}

// locked door interactions
bool doorUnlocked = false;

function lockedDoor_Look()
{
  cMick.Say("The door is obviously locked..");
  cMick.Say("WHY IS THIS HAPPENING?!?");
  cMick.Say("STOP PUTTING ME BACK IN THIS DAMN ROOM!!");
}
function lockedDoor_Interact()
{
  cMick.Walk(748, 510, eBlock);
  if (doorUnlocked) {
    RestoreWalkableArea(2);
    cMick.Say("I hate S.A.L...");
    cMick.Say("ugh.. let's just go..");
    aDoor_open.Play(eAudioPriorityHigh);
    cMick.Walk(748, 510, eBlock);
    cMick.LoseInventory(iBedspring);
    player.ChangeRoom(2);
  }
  else {
    aDoor_locked.Play(eAudioPriorityHigh);
    cMick.Say("Come on.. I can do this with my eyes closed by now..");
    cMick.Say("I can get the bedspring from under the pillow, then use it to unlock the door..");
  }
}
function lockedDoor_UseInv()
{
  if (player.ActiveInventory == iBedspring) {
    cMick.Walk(748, 510, eBlock);
    aPick_lock.Play(eAudioPriorityHigh);
    cMick.LoseInventory(iBedspring);
    cMick.Say("Great.. it's open.. again..");
    cMick.Walk(630, 480, eBlock);
    doorUnlocked = true;
    salHelloDialog = true;
  }
}

// bedspring interactions
function bedspring_Interact()
{
  cMick.Walk(770, 640, eBlock);
  cMick.Say("The bedspring never fails...");
  cMick.AddInventory(iBedspring);
  bedspring.Visible = false;
  cMick.Say("Now to unlock that door");
}
function bedspring_Look()
{
  cMick.Say("I need to unlock the door with this bedspring");
}

// flashlight interactions
function flashlight_Look()
{
  cMick.Say("Hmm.. The flashlight is on the floor again..");
  cMick.Say("Didn't I pick it up last time?");
}
function flashlight_Interact()
{
  cMick.Walk(511, 632, eBlock);
  cMick.Say("Should come in handy..");
  cMick.AddInventory(iFlashlight);
  flashlight.Visible = false;
}


// hidden door interactions
function hiddenDoor_Look()
{
  cMick.Say("Another door?");
  cMick.Say("It was here all this time?!");
}
function hiddenDoor_Interact()
{
  cMick.Say("Give me an E!");
  cMick.Say("Give me an X!");
  cMick.Say("Give me a C!");
  cMick.Say("Give me an I!");
  cMick.Say("Give me a T!");
  cMick.Say("Give me an E!");
  cMick.Say("Give me a D!");
  cMick.Say("What do we get?!");
  cMick.Say("Meh.. let's just go...");
  cMick.Walk(120, 730, eBlock);
  player.ChangeRoom(5);
}

// room events
function room_Load()
{
  cMick.ChangeView(6);
  RemoveWalkableArea(2);
  if (justDied) {
    if (cMick.HasInventory(iFlashlight)) {
      cMick.LoseInventory(iFlashlight);
    }
    flashlight.Visible = true;
    bedspring.Visible = false;
    doorUnlocked = false;
    cMick.Baseline = 2000;
    cMick.x = 750;
    cMick.y = 660;
  }
  else {
    cMick.Baseline = 2000;
    cMick.x = 1020;
    cMick.y = 730;
  }
  if (gameEnd) {
    hiddenDoor.Visible = true;
  }
  
  bgAudio.Play(eAudioPriorityLow);
}
function room_AfterFadeIn()
{
  if (justDied && !gameEnd) {
    cMick.Say("Oh god, not again...");
    cMick.Say("I'm so tired of this!");
    cMick.Say("Every time with this damn room..");
    justDied = false;
  }
  else if (justDied && gameEnd){
    cMick.Say("Oh god, not again...");
    cMick.Say("I'm so tired of this!");
    cMick.Say("Wait a second.. that door wasn't there before..");
  }
  else {
    cMick.Walk(780, 730, eBlock);
  }
}

function hGianPeach_AnyClick()
{
  cMick.Say("It's a giant peach...");
  cMick.Say("No one ever comes in to water it");
  cMick.Say("How come it always looks so fresh?");
}

function hLightBulb_Look()
{
  cMick.Say("The light is always on in here");
  cMick.Say("I can't find any way to turn it off...");
}

function hCabinet_Look()
{
  cMick.Say("Looks like a cabinet, doesn't it?");
}

function hCabinet_Interact()
{
  cMick.Say("It's actually just a cardboard prop..");
  cMick.Say("I can't open any of the drawers...");
}

function hTowel_Look()
{
  cMick.Say("Don't forget to bring a towel");
}

function hTowel_Interact()
{
  cMick.Say("Nah.. I don't really need it, do I?");
}

function room_FirstLoad()
{
  gIconbar.Visible = true;
  initialize_control_panel();
}
