// room script file

// pillow interactions
function hHotspot1_Look()
{
  if (!cMick.HasInventory(iBedspring)) {
    cMick.Say("Yup, gotta move that pillow to find the spring again..");
  }
  else {
    cMick.Say("Nothing else under the pillow");
  }
}
function hHotspot1_Interact()
{
  cMick.Walk(800, 620, eBlock);
  if (!cMick.HasInventory(iBedspring)) {
    bedspring.Visible = true;
    cMick.Say("And there it is! What a surprise..");
  }
  else {
    cMick.Say("Nothing else under the pillow");
  }
}

// locked door interactions
bool doorUnlocked = false;

function lockedDoor_Look()
{
  cMick.Say("The door is obviously locked..");
  cMick.Say("WHY IS THIS HAPPENING?!?");
  cMick.Say("STOP PUTTING ME BACK IN THIS DAMN ROOM!!");
}
function lockedDoor_Interact()
{
  cMick.Walk(890, 730, eBlock);
  if (doorUnlocked) {
    cMick.Say("I hate S.A.L...");
    cMick.Say("ugh.. let's just go..");
    cMick.LoseInventory(iBedspring);
    player.ChangeRoom(2);
  }
  else {
    cMick.Say("Come on.. I can do this with my eyes closed by now..");
    cMick.Say("I can get the bedspring from under the pillow, then use it to unlock the door..");
  }
}
function lockedDoor_UseInv()
{
  if (player.ActiveInventory == iBedspring) {
    cMick.Walk(890, 730, eBlock);
    cMick.Say("Great.. it's open.. again..");
    doorUnlocked = true;
    salHelloDialog = true;
  }
}

// bedspring interactions
function bedspring_Interact()
{
  cMick.Walk(800, 620, eBlock);
  cMick.Say("The bedspring never fails...");
  cMick.AddInventory(iBedspring);
  bedspring.Visible = false;
  cMick.Say("Now to unlock that door");
}
function bedspring_Look()
{
  cMick.Say("I need to unlock the door with this bedspring");
}

// flashlight interactions
function flashlight_Look()
{
  cMick.Say("Hmm.. The flashlight is on the floor again..");
  cMick.Say("Didn't I pick it up last time?");
}
function flashlight_Interact()
{
  cMick.Walk(188, 670, eBlock);
  cMick.Say("Should come in handy..");
  cMick.AddInventory(iFlashlight);
  flashlight.Visible = false;
}


// hidden door interactions
function hiddenDoor_Look()
{
  cMick.Say("Another door?");
  cMick.Say("It was here all this time?!");
}
function hiddenDoor_Interact()
{
  cMick.Say("Give me an E!");
  cMick.Say("Give me an X!");
  cMick.Say("Give me a C!");
  cMick.Say("Give me an I!");
  cMick.Say("Give me a T!");
  cMick.Say("Give me an E!");
  cMick.Say("Give me a D!");
  cMick.Say("What do we get?!");
  cMick.Say("Meh.. let's just go...");
  cMick.Walk(120, 730, eBlock);
  player.ChangeRoom(5);
}

// room events
function room_Load()
{
  if (justDied) {
    if (cMick.HasInventory(iFlashlight)) {
      cMick.LoseInventory(iFlashlight);
    }
    flashlight.Visible = true;
    bedspring.Visible = false;
    doorUnlocked = false;
    cMick.x = 750;
    cMick.y = 650;
  }
  else {
    cMick.x = 1040;
    cMick.y = 730;
  }
  if (gameEnd) {
    hiddenDoor.Visible = true;
  }
}
function room_AfterFadeIn()
{
  if (justDied) {
    cMick.Say("Oh god, not again...");
    cMick.Say("I'm so tired of this!");
    cMick.Say("Every time with this damn room..");
    justDied = false;
  }
  else {
    cMick.Walk(880, 730, eBlock);
  }
}


